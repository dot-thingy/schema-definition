Thingy {
  // OVERALL SCHEMA
  ThingyDefinitionExpr = Comment* ThingySchema Comment*
  ThingySchema = ApplicationDefinition ThingySchemaDeclarationExpr*
  ThingySchemaDeclarationExpr = ThingySchemaDeclarations | Comment
  ThingySchemaDeclarations = IncludeDefinition | ConfigDefinition | ComponentDefinition | SourceDefinition | EndpointDefinition

  // COMMON TYPES
  PascalCased = upper alnum*
  ThingyThingDeclaration = PascalCased
  QuoteChar = ((~"\\" "'") | (~"\\" "\""))
  Quoted<x> = QuoteChar x QuoteChar
  QuotedFilePath = Quoted<FilePath>
  EndpointPath = "/" alnum* ("/" alnum*)*
  FilePath = alnum* ("/" alnum+)* ("." alnum+)*
  Comment = "//" (~"\n" any)* "\n"
  AssignmentChar = "="
  AssignmentExpr<assignee, evaluator> = assignee AssignmentChar evaluator
  ComputedValue = ComputedValueExpr | Quoted<ComputedValueExpr>
  ComputedValueExpr = (any ComputedKeywords* any*)+
  ComputedKeywords = KeywordStringInterpolation
  KeywordStringInterpolation = "${" KeywordStringInterpolationInput "}"
  KeywordStringInterpolationInput = Any+
  Any = any

  ////////////////////////////
  // GLOBAL TYPES AVAILABLE //
  ////////////////////////////

  // APPLICATION
  ApplicationDefinition = "Application" ApplicationName "{" ApplicationExpr "}"
  ApplicationName = ThingyThingDeclaration
  ApplicationExpr = (ApplicationProperty | Comment)*
  ApplicationProperty = ApplicationPropDisplayNameExpr
  // display name
  ApplicationPropDisplayNameExpr = AssignmentExpr<"DisplayName", Quoted<ApplicationPropDisplayName>>
  ApplicationPropDisplayName = (~QuoteChar Any)*

  // CONFIG OPTIONS
  ConfigDefinition = "Config" ConfigName "{" ConfigExpr "}"
  ConfigName = ThingyThingDeclaration
  ConfigExpr = (ConfigPropertyExpr | Comment)*
  // property defs
  ConfigPropertyExpr = AssignmentExpr<ConfigPropertyName, ConfigValue>
  ConfigPropertyName = ThingyThingDeclaration
  ConfigValue = (StaticConfigValue | ComputedConfigValue)
  StaticConfigValue = (StaticConfigValueDigit | Quoted<StaticConfigValueString>)
  StaticConfigValueDigit = (~"\n" digit)+
  StaticConfigValueString = (~QuoteChar Any)*
  ComputedConfigValue = ComputedValue

  // INCLUDES
  IncludeDefinition = "Include" FilePath

  // STORAGES
  // StorageDefinition = "Storage" StorageName "{" ConnectionOptions "}"
  // StorageName = ThingyThingDeclaration
  // ConnectionOptions = (ConnectionOption | Comment | Any)*
  // ConnectionOptionPropertyName = ThingyThingDeclaration
  // ConnectionOption = ConnectionOptionPropertyName AssignmentChar alnum+ spaces?

  // COMPONENTS
  ComponentDefinition = "Component" ComponentName "{" ComponentExpr "}"
  ComponentName = ThingyThingDeclaration
  ComponentExpr = AssignmentExpr<"source", ComponentSource>
  ComponentSource = QuotedFilePath

  // SOURCES
  SourceDefinition = "Source" SourceName "{" SourceExpr "}"
  SourceName = ThingyThingDeclaration
  SourceExpr = SourceTypeExpr SourceSourceExpr
  // type
  SourceTypeExpr = AssignmentExpr<"type", SourceType>
  SourceType = ("Function" | "Component")
  // source
  SourceSourceExpr = AssignmentExpr<"source", SourceSource>
  SourceSource = (ComponentName | QuotedFilePath)

  // ENDPOINTS
  EndpointDefinition = "Endpoint" EndpointName "{" EndpointExpr "}"
  EndpointName = ThingyThingDeclaration
  EndpointExpr = EndpointMethodExpr EndpointPathExpr EndpointUseExpr
  // method
  EndpointMethodExpr = AssignmentExpr<"method", EndpointMethod>
  EndpointMethod = "GET" | "POST" | "PUT" | "DELETE"
  // path
  EndpointPathExpr = AssignmentExpr<"path", EndpointPathDef>
  EndpointPathDef = Quoted<EndpointPath>
  // use
  EndpointUseExpr = AssignmentExpr<"use", EndpointUseComponentExpr>
  EndpointUseComponentExpr = ComponentName
}
